# Zamborszky, Hong, Csikasz-Nagy: 
# Computational Analysis of Mammalian Cell Division Gated by a Circadian Clock: Quanized Cell Cycles and Cell Size Control


# parameters for Wee1 regulation
par kw5'=0.25, kw5"=2, kw6=1

# parameters for Wee1 phosphorylation
par kw2'=0.2, kw2"=2, Jw2=0.2, kw1=0.4, Jw1=0.2, kwd=1

# parameters for Cdc25 regulation
par kc3'=0.1, kc3"=1, Jc3=0.05, Jc4=0.05, kc4=0.4

# parameters for CycB regulation
par K1'=0.1, K1=0.6, J1=0.1, K2'=0.05, K2=20, K2"=1

#parameters for CycB phosphorylation
par kwee1'=0.08, kwee1"=10, kcdc25'=0.05, kcdc25"=10

# parameters for ERG
par k15=0.25, k16=0.25, J15=0.1

# parameters for DRG
par k17'=0.35, k17=10, J17=0.3, k18=10

# parameters for CycD
par K9=2.5, K10=5, k24=1000, k24r=10

# parameters for CycE
par K7'=0, K7=0.6, K8'=0.1, K8=2, K25=1000, K25R=10, J8=0.1, YE=1, YB=0.05

# parameters for CycA
par K29=0.05, K30=20

# parameters for p27
par K5=20, K6'=10, K6=100, HE=0.5, HB=1, HA=0.5

# parameters for Rb
par RBT=10, LD=3.3, LE=5, LB=5, LA=3, K20=10, K19'=0, K19=20

# parameters for PP1A
par K21=1, PP1T=1, FE=25, FB=2

# parameters for Cdh1
par K3'=7.5, K3=140, J3=0.01, J4=0.01, K4=40, GE=0, GB=1, GA=0.3

# parameters for PPX
par K33=0.05, K34=0.05

# parameters for IEP activation/inactivation
par K31=0.7, K32=1.8, J31=0.01, J32=0.01

# parameters for Cdc20 activation/inactivation
par K13=5, K14=2.5, J13=0.005, J14=0.005

# parameters for Cdc20T synthesis/degradation
par K11'=0, K11=1.5, K12=1.5

# parameters for E2F
par E2FT=5, K22=1, K23'=0.005, K23=1

# parameters for E2F:Rb
par K26=10000, K26R=200

# parameters for GM
par K27=0.2, K28=0.2

# specific growth rate
par MU=0.033

# parameter that regulates cell division
par Kez=0.2

# parameter for the time of growth factor deprivation
par DEPRIV=-10


# Rate equations

# max rate for Cdh1 inactivation
V4=K4*(GE*CYCE+GA*CYCA+GB*CYCB)

# rate of p27 degradation
V6=K6'+K6*(HE*CYCE+HA*CYCA+HB*CYCB)

# rate of CycE degradation
V8=K8'+K8*(YE*(CYCE+CYCA)+YB*CYCB)/(J8+CYCET)

# calculation of active form of PP1
PP1A=PP1T/(K21*(FE*(CYCE+CYCA)+FB*CYCB)+1)

# calculation of hypo-phosphorylated form of Rb
RBH=RBT/(K20*(LD*CYCDT+LE*CYCE+LA*CYCA+LB*CYCB)/(K19'*(PP1T-PP1A)+K19*PP1A)+1)

# calculation of E2F and Rb complex
L = (K26R+K20*(LD*CYCDT+LE*CYCE+LA*CYCA+LB*CYCB))/K26
E2RBC  = 2*E2FT*RBH/(E2FT+RBH+L+sqrt((E2FT+RBH+L)^2-4*E2FT*RBH))

# calculation of active form of E2F
E2FA=(E2FT-E2RBC)*E2F/E2FT

# rate of CycB degradation
V2=K2'*(1-Cdh1) + K2*Cdh1 + K2"*Cdc20

# Total concentrations of cyclinE, cyclinD, CyclinA, P27, Cdc20
aux CYCET=CYCE+CE
CYCET=CYCE+CE
aux CYCDT=CYCD+CD
CYCDT=CYCD+CD
aux CYCAT=CYCA+CA
aux P27T=P27+CD+CE+CA
aux Cdc20T = Cdc20 + Cdc20i

# noise amplitude
par amp=0.005

# creation of random numbers with the Wiener process function
wiener w1
wiener w2
wiener w3
wiener w4
wiener w5
wiener w6
wiener w7
wiener w8
wiener w9
wiener w10
wiener w11
wiener w12
wiener w13
wiener w14
wiener w15
wiener w16
wiener w17
wiener w18
wiener w19
wiener w20
wiener w21


# Differential equations
# global functions are necessary to keep variables positive 
# (random noise with rounding errors created some cases whe this caused a problem)


# cell cycle module

CYCB' = eps*(K1'+K1*(CYCB/J1)^2/(1+(CYCB/J1)^2))*MASS - V2*CYCB - (kwee1'+kwee1"*Wee1)*CycB + (kcdc25'+kcdc25"*Cdc25a)*CycBP + w6*(2*amp*CYCB)^0.5
GLOBAL -1 Cycb {Cycb=0.0001}

CycBP' = (kwee1'+kwee1"*Wee1)*CycB - (kcdc25'+kcdc25"*Cdc25a)*CycBP - V2*CYCBP + w7*(2*amp*CycBP)^0.5
GLOBAL -1 Cycbp {Cycbp=0.0001}

Wee1' = (kw5'+kw5"*M)-kw6*Wee1 - ((kw2'+kw2"*CycB)*Wee1)/(Jw2+Wee1) + (kw1*Wee1P)/(Jw1+Wee1P) + w10*(2*amp*Wee1)^0.5
GLOBAL -1 wee1 {wee1=0.0001}

Wee1P' = ((kw2'+kw2"*CycB)*Wee1)/(Jw2+Wee1) - (kw1*Wee1P)/(Jw1+Wee1P) - kwd*Wee1P + w9*(2*amp*Wee1P)^0.5
GLOBAL -1 wee1p {wee1p=0.0001}

Cdc25a' = ((kc3'+kc3"*CycB)*(1-Cdc25a))/(Jc3+(1-Cdc25a)) - (kc4*Cdc25a)/(Jc4+Cdc25a) + w8*(2*amp*Cdc25a)^0.5
GLOBAL -1 cdc25a {Cdc25a=0.0001}
GLOBAL +1 {Cdc25a-1} {Cdc25a=0.99}

ERG' = eps*k15/(1+(DRG/J15)^2) - k16*ERG + w17*(2*amp*ERG)^0.5
GLOBAL -1 erg {erg=0.0001}

DRG' = eps*(k17'*ERG+k17*(DRG/J17)^2/(1+(DRG/J17)^2)) - k18*DRG + w15*(2*amp*DRG)^0.5
GLOBAL -1 drg {drg=0.0001}

CYCD' = eps*K9*DRG + V6*CD + k24r*CD - k24*CYCD*P27 - K10*CYCD + w11*(2*amp*CYCD)^0.5
GLOBAL -1 Cycd {Cycd=0.0001}

CD' = k24*CYCD*P27 - k24r*CD - V6*CD - K10*CD + w12*(2*amp*CD)^0.5
GLOBAL -1 Cd {Cd=0.0001}

CYCE' = eps*(K7'+K7*E2FA) - V8*CYCE - K25*CYCE*P27 + K25R*CE + V6*CE + w13*(2*amp*CYCE)^0.5
GLOBAL -1 Cyce {Cyce=0.0001}

CE' = K25*CYCE*P27 - K25R*CE - V8*CE - V6*CE + w14*(2*amp*CE)^0.5
GLOBAL -1 Ce {Ce=0.0001}

CYCA' = eps*K29*E2FA*MASS - K30*Cdc20*CYCA - K25*CYCA*P27 + K25R*CA + V6*CA + w4*(2*amp*CYCA)^0.5
GLOBAL -1 Cyca {Cyca=0.0001}

CA' = K25*CYCA*P27 - K25R*CA - (V6+K30*Cdc20)*CA + w5*(2*amp*CA)^0.5
GLOBAL -1 Ca {Ca=0.0001}

P27' = eps*K5 + K10*CD + k24r*CD - V6*P27 - K25*P27*(CYCE+CYCA) - k24*CYCD*P27 + K25R*(CE+CA) + V8*CE + K30*CDC20*CA + w20*(2*amp*P27)^0.5
GLOBAL -1 p27 {p27=0.0001}

Cdh1' = (K3'+K3*Cdc20)*(1-Cdh1)/(J3+1-Cdh1) - V4*Cdh1/(J4+Cdh1) + w3*(2*amp*Cdh1)^0.5
GLOBAL -1 Cdh1 {Cdh1=0.0001}
GLOBAL +1 {Cdh1-1} {Cdh1=0.99}

PPX' = eps*K33 - K34*PPX + w21*(2*amp*PPX)^0.5
GLOBAL -1 ppx {ppx=0.0001}

IEP' = K31*CYCB*(1-IEP)/(J31+1-IEP) - K32*PPX*IEP/(J32+IEP) + w19*(2*amp*IEP)^0.5
GLOBAL -1 iep {iep=0.0001}
GLOBAL +1 {IEP-1} {IEP=0.99}

Cdc20' = K13*IEP*(Cdc20i)/(J13+Cdc20i) - K14*Cdc20/(J14+Cdc20) - K12*Cdc20 + w1*(2*amp*Cdc20)^0.5
GLOBAL -1 Cdc20 {Cdc20=0.0001}

Cdc20i' = eps*(K11'+K11*CYCB) - K13*IEP*(Cdc20i)/(J13+Cdc20i) + K14*Cdc20/(J14+Cdc20) - K12*Cdc20i +w2*(2*amp*Cdc20i)^0.5
GLOBAL -1 Cdc20i {Cdc20i=0.0001}

E2F' = K22*(E2FT-E2F) - (K23'+K23*(CYCA+CYCB))*E2F + w16*(2*amp*E2F)^0.5
GLOBAL -1 e2f {e2f=0.0001}

GM' = k27*MASS*HEAV(0.8-RBH/RBT) - K28*GM + w18*(2*amp*GM)^0.5
GLOBAL -1 gm {gm=0.0001}

# growth rate is assumed to be robust for fluctuations
MASS' = eps*MU*GM




#The following two statements implement cell division whenever the variable CycB crosses 0.2 from above
# oszt variable is needed to avoid a second division when random noise drives CycB through 0.2 twice in a short time window

GLOBAL -1 {CycB-oszt} {mass=0.5*mass; oszt=-8; GM=0.5*GM}
GLOBAL -1 {CycB-(kez+0.1)} {oszt=kez}

oszt' = 0

#The following two statements implement growth factor deprivation for the time window: DEPRIV < t < DEPRIV+10
GLOBAL +1 {t-DEPRIV} {eps=0.5}
GLOBAL +1 {t-DEPRIV-10} {eps=1}

eps' = 0



# circadian rhythm module

# parameters for the messenger RNA of the clock protein
par n=2, J=0.3, kms=1, kmd=0.1

# parameters for the clock protein
par kcps=0.5, kcpd=0.525, ka=100, kd=0.01, kp1=10

# parameters for the dimer clock protein
par kcp2d=0.0525, kicd=0.01, kica=20

# parameters for the transcription factor of M
par kp2=0.1, Jp=0.05

par TFtot=0.5

# differential equations
# messenger RNA of the clock proteins
M'=kms*TF^n/(J^n+TF^n)-kmd*M

# monomer clock proteins
CP'=kcps*M-kcpd*CP-2*ka*CP^2+2*kd*CP2-kp1*CP/(Jp+CP+2*CP2+2*IC)

# dimer clock proteins
CP2'=ka*CP^2-kd*CP2-kcp2d*CP2+kicd*IC-kica*CP2*TF-kp2*CP2/(Jp+CP+2*CP2+2*IC)

# transcription factor of clock mRNA
TF'=kcp2d*IC+kicd*IC-kica*TF*CP2+kp2*IC/(Jp+CP+2*CP2+2*IC)

# inactive complex of CP2 and TF
aux IC= TFtot - TF
IC= TFtot - TF


# initial conditions

init Wee1=2.3
init Wee1P=0.077
init Cdc25a=0.016
init CycB=0.0025
init CycBP=0.0028
init ERG=0.01218087133020163
init DRG=0.9005327820777893
init CYCD=0.012876115970612
init CD=0.434299469
init CYCE=0.01693358868360519
init CE=0.4828373789787
init CYCA=0.001462333556264639
init CA=0.0403826870955527
init P27=0.8491207979619503
init Cdh1=0.99
init PPX=1
init IEP=0.000017
init Cdc20=0.00001
init Cdc20i=0.00009
init E2F=4.957176518440247
init GM=0.93809711933136
init MASS=1.07598838806
init eps=1
init oszt=8
init M=1.4
init CP=0.037
init CP2=0.046
init TF=0.13

# setup for simulation methods

@ YP=cycb, TOTAL=500, METH=Euler, njmp=1000, toler=0.00001, dt=0.0005, XHI=500, YLO=0, YHI=3, BOUND=100000, maxstore=1000000


done
